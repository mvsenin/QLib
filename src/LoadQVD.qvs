/*
	Loads the specified QVD file $(pLoadQVD_FileName) into Qlik table
	in the $(pLoadQVD_QlikTableName), if specified, otherwise, the QVW file
	name is used as the table name.
	Existing Qlik table can either be replaced (bydefault) or added,
	depending of the mode.

    Parameters:
     - @pLoadQVD_FileName - file full path
     - @pLoadQVD_QlikTableName - Qlik table name
	 - @pLoadQVD_LoadMode - load mode:
		- r or R - replace (default)
		- a or A - append (concatenate)

	Usage sample:
		Call LoadQVD('$(sDestinationFile)', 'tmpData');
*/
Sub LoadQVD(pLoadQVD_FileName, pLoadQVD_QlikTableName, pLoadQVD_LoadMode)

	// Check, whether the QVD exists
	if $(FileExists($(pLoadQVD_FileName))) then
		// Get table name
		If Len(pLoadQVD_QlikTableName) < 1 then
			pLoadQVD_QlikTableName
				= SubField(SubField(pLoadQVD_FileName, '/', -1), '.', 1);
		End If

		// Get mode - A is only possible if the table does already exist
		pLoadQVD_LoadMode = Upper(pLoadQVD_LoadMode);
		If Len(pLoadQVD_LoadMode) < 1 then
			pLoadQVD_LoadMode = 'R';
		ElseIf 'A' = pLoadQVD_LoadMode and IsNull(TableNumber('$(pLoadQVD_QlikTableName)')) then
			pLoadQVD_LoadMode = 'R';
		End If
		
		// Check whether table exists and should the table be replaced or not
		If TableNumber('$(pLoadQVD_QlikTableName)') >=0 and 'R' = pLoadQVD_LoadMode then
			Trace [LoadQVD] Dropping the table: $(pLoadQVD_QlikTableName);
			Drop Table [$(pLoadQVD_QlikTableName)];
		End If

		// Get table name clause
		If 'R' = pLoadQVD_LoadMode then
			pLoadQVD_QlikTableName = '[$(pLoadQVD_QlikTableName)]:';
		ElseIf 'A' = pLoadQVD_LoadMode then
			pLoadQVD_QlikTableName = 'Concatenate([$(pLoadQVD_QlikTableName)])';
		End If

		// Load the QVD in to the table
		$(pLoadQVD_QlikTableName)
		Load * From [$(pLoadQVD_FileName)] (qvd);
	Else
		Trace [LoadQVD] There is no file: $(pLoadQVD_FileName);
	End If
	
  	pLoadQVD_FileName=;
    pLoadQVD_QlikTableName=;
	pLoadQVD_LoadMode=;
EndSub;
